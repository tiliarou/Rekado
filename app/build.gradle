apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion currentSDKVersion
    ndkVersion NDKVersion

    defaultConfig {
        applicationId "com.pavelrekun.rekado"
        minSdkVersion minimumSDKVersion
        targetSdkVersion currentSDKVersion
        versionCode 73
        versionName "4.1"

        archivesBaseName = "[$versionName] Rekado [${versionCode}]"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    lintOptions {
        abortOnError false
    }

    aaptOptions {
        noCompress 'bin'
    }

    viewBinding {
        enabled = true
    }

}

dependencies {

    // Local
    implementation(name: 'magta', ext: 'aar')
    implementation(name: 'penza', ext: 'aar')

    // Google: AndroidX
    implementation "androidx.recyclerview:recyclerview:$androidXRecyclerViewVersion"
    implementation "androidx.browser:browser:$androidXBrowserVersion"
    implementation "androidx.preference:preference:$androidXPreferencesVersion"
    implementation "androidx.constraintlayout:constraintlayout:$androidXConstraintLayoutVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidXLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidXLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$androidXLifecycleVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$androixXNavigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$androixXNavigationVersion"

    // Google: AndroidX KTX
    implementation "androidx.core:core-ktx:$androidXCoreKTX"

    // Google: Design
    implementation "com.google.android.material:material:$materialComponentsVersion"
    implementation "com.google.android:flexbox:$flexBoxLayoutVersion"

    // Design
    implementation "de.halfbit:edge-to-edge:$edgeToEdgeVersion"

    // Tools
    implementation "org.greenrobot:eventbus:$eventBusVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.journeyapps:zxing-android-embedded:$zxingVersion"
    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation "com.github.javiersantos:AppUpdater:$appUpdaterVersion"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutinesVersion"

}